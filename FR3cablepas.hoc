// Cat FR motoneuron model (reduced morphology)
//
// This file constructs the equivalent cylinder of the 3D dendritic morphology of cat FR MN (cell 43/5 in Cullheim et al., 1987)
// The reduced model reproduce the passive properties of the full morphology faithfully
// Reduced model passive properties:    Rin = 1.38 MOhm, tau(0)=6.8 ms,  tau(1) = 1.5 ms (graphical peeling method)
// Experimental data of same MN:        Rin = 1.4 MOhm,  tau(0)=6.82 ms, tau(1) = 1.57 ms
//
// Sherif Elbasiouny
// 07/01/2009
// modified to have 3 dendrites
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

load_file("nrngui.hoc")					// Load Neuron

create soma, is, d1,d2,d3, axonhillock
access soma

objref dend
dend=new SectionList()
d1 dend.append()
d2 dend.append()
d3 dend.append()



// Define anatomical and biophysical properties of each section

soma {
	nseg = 2		      // 1 compartment
	L = 48.8	            // [um] length
	diam = 48.8	            // [um] diameter

 	insert pas	            // standard passive current
	g_pas = 1/225	      // [S/cm^2] conductance for leak current
	e_pas = -70.0	      // [mV] equilibrium potential for leak
}


is {
	nseg = 5		      // 5 compartment (changed by RP for better behavior)
	L = 30			// [um] length
	diam = 3.3		      // [um] diameter

	insert pas	            // standard passive current
	g_pas = 1/1000   // [S/cm^2] conductance for leak current; changed from 225 by RP
	e_pas = -70			// [mV] equilibrium potential for leak
}


axonhillock {
	nseg = 11		      // 11 compartments
	L = 15			// [um] length; changed by RP to reflect Conradi
	diam(0:1) = 3.3:13		// [um] diameter increase

	insert pas	            // standard passive current
	g_pas = 1/1000    // [S/cm^2] conductance for leak current; changed from 225 by RP
	e_pas = -70			// [mV] equilibrium potential for leak
}


forsec dend {
	nseg = 10		      // 10 compartments
	L = 4635		      // [um] total length of the equivalent dendrite
	diam =19.2		      // [um] diameter of the stem
	diam(0.3:1) = 19.2:1      // [um] tapering of the distal dendrite

	insert pas         	// standard passive current
	g_pas = 1/11000	     	// [S/cm^2] conductance for leak current
	e_pas = -70		      // [mV] equilibrium potential for leak
}

forall {
	Ra = 70          		// [ohm.cm]
	cm = 1          		// [uf/cm2]
}

// connect model sections
connect is(1), axonhillock(0)
connect axonhillock(1), soma(0)
connect d1(0),1
connect d2(0),1
connect d3(0), 0.5

